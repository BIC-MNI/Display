dnl Process this file with autoconf to produce a configure script.
AC_INIT
AC_CONFIG_SRCDIR([markers/markers.c])
AC_PREREQ(2.58)
AM_INIT_AUTOMAKE(Display,1.3.7)
AC_CONFIG_HEADERS([config.h])


dnl Optional workaround for buggy compiler.
AC_ARG_ENABLE([menu-fallback],
[  --disable-menu-fallback    Omit compiled-in fallback for menu.],
[if test "$enableval" = no; then
    AC_DEFINE(DISPLAY_DISABLE_MENU_FALLBACK,1,
              [define to disable compiled-in fallback menu])
 fi
])


dnl Checks for programs.
AC_PROG_CC
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB

dnl Use libtool for linking
AM_PROG_LIBTOOL

dnl Find all the X stuff
AC_PATH_XTRA

dnl Add extra include or link directories
smr_WITH_BUILD_PATH

dnl Checks for header files.


dnl Checks for libraries.

smr_REQUIRED_LIB(m,sqrt,math.h)
smr_REQUIRED_LIB(netcdf,ncopen,netcdf.h)
smr_REQUIRED_LIB(minc,miicv_create,minc.h)
smr_REQUIRED_LIB(volume_io,input_volume,volume_io.h)
smr_REQUIRED_LIB(bicpl,input_graphics_file,bicpl.h)

MNI_LIBS="$LIBS"
LIBS=

smr_REQUIRED_LIB(GL,glBegin,GL/gl.h)
smr_REQUIRED_LIB(GLU,gluLookAt,GL/glu.h)
smr_REQUIRED_LIB(glut,glutInit,GL/glut.h,
                 [$X_LIBS $X_PRE_LIBS -lX11 -lXmu $X_EXTRA_LIBS])

GL_LIBS="$LIBS"
LIBS=

AC_SUBST(MNI_LIBS)
AC_SUBST(GL_LIBS)


dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

dnl Checks for library functions.
smr_OPTIONAL_LIB(malloc,mallinfo,malloc.h)
if test $HAVE_libmalloc = yes; then
    AC_DEFINE(HAVE_MALLINFO,1,[define if function mallinfo() is available])
    malloc_LIBS=-lmalloc
    AC_SUBST(malloc_LIBS)
fi


AC_CONFIG_FILES([Makefile
Documentation/Makefile
Graphics/Makefile
Graphics/GLUT_windows/Makefile
Graphics/G_graphics/Makefile
Graphics/OpenGL_graphics/Makefile
atlas/Makefile
callbacks/Makefile
current_obj/Makefile
cursor/Makefile
cursor_contours/Makefile
edit_surface/Makefile
events/Makefile
images/Makefile
immediate_mode/Makefile
input_files/Makefile
intersect/Makefile
main/Makefile
markers/Makefile
menu/Makefile
segmenting/Makefile
slice_window/Makefile
structures/Makefile
surface_curves/Makefile
surface_extraction/Makefile
tubes/Makefile
voxel_scan/Makefile
])
AC_OUTPUT
