dnl Process this file with autoconf to produce a configure script.A
AC_INIT([Display], [1.6.0], [robert.d.vincent@mcgill.ca])
AC_CONFIG_SRCDIR([markers/markers.c])
AC_CONFIG_AUX_DIR(ac_config_aux)
AC_PREREQ(2.57)
AM_INIT_AUTOMAKE
AC_CONFIG_HEADERS([config.h])

dnl Optional workaround for buggy compiler.
AC_ARG_ENABLE([menu-fallback],
[  --disable-menu-fallback    Omit compiled-in fallback for menu.],
[if test "$enableval" = no; then
    AC_DEFINE(DISPLAY_DISABLE_MENU_FALLBACK,1,
              [define to disable compiled-in fallback menu])
 fi
])

dnl Optional support for SEAL.
AC_ARG_WITH([seal],
[  --with-seal              Enable support for sulcal labelling using SEAL.])
AM_CONDITIONAL(USE_SEAL, test x$with_seal = xyes)

dnl Checks for programs.
AC_PROG_CC
AM_PROG_CC_C_O
AC_PROG_LN_S
AC_PROG_MAKE_SET

dnl Use libtool for linking
AM_PROG_LIBTOOL

dnl Find all the X stuff
# AC_PATH_XTRA

AC_CHECK_HEADER([math.h])
AC_CHECK_LIB(m,sqrt)
AC_CHECK_LIB(netcdf,ncopen, [], [AC_MSG_ERROR([-lnetcdf not found])])
AC_CHECK_LIB(hdf5,H5open, [], [AC_MSG_ERROR([-lhdf5 not found])])
AC_CHECK_HEADER([minc2.h], [], [
                           AC_MSG_ERROR([Could not find minc2.h])
                           ])
AC_CHECK_LIB(minc2,miget_version, [], [
                                  AC_MSG_ERROR([Could not find -lminc2])
                                  ])
AC_CHECK_HEADER([bicpl.h], [], [
                           AC_MSG_ERROR([Could not find bicpl.h])
                           ])
AC_CHECK_LIB(bicpl,input_globals_file, [], [
                                       AC_MSG_ERROR([Could not find -lbicpl])
                                       ])
                                       
if test x$with_seal = xyes ; then
   AC_CHECK_HEADERS([bicseal/SULGRAPHDATA.h], [bicseal/prototypes.h], [],
   [AC_MSG_ERROR([Could not find SEAL headers.])])
   AC_CHECK_LIB(bicseal, [match_indice_with_enum(0,0)], [], 
                         [AC_MSG_ERROR([Could not find SEAL library.])])
   AC_DEFINE(DISPLAY_USE_SEAL,1,
              [define to enable sulcal labelling using SEAL])
fi

dnl get OpenGL stuff
AC_CHECK_LIB(GL, glGetString, [], [AC_MSG_ERROR([Can't find OpenGL])])
AC_CHECK_LIB(glut, glutGet, [], [AC_MSG_ERROR([Can't find FreeGLUT])])

dnl Checks for library functions.
AC_CHECK_LIB(malloc,mallinfo,[
    AC_DEFINE(HAVE_MALLINFO,1,[define if function mallinfo() is available])
    malloc_LIBS=-lmalloc
    AC_SUBST(malloc_LIBS)
])

AC_DEFINE([HAVE_MINC1],[1],[Include MINC 1.0 support])
AC_DEFINE([HAVE_MINC2],[1],[Include MINC 2.0 support])
AC_DEFINE([PROJECT_NAME],[PACKAGE], [Name of the project])
AC_DEFINE([PROJECT_VERSION],[VERSION], [Version of the project])
AC_DEFINE_UNQUOTED(HARD_CODED_DISPLAY_DIRECTORY1,"${prefix}/bin", [Search directory 1])
AC_DEFINE_UNQUOTED(HARD_CODED_DISPLAY_DIRECTORY2,"${prefix}/lib", [Search directory 2])
# AC_DEFINE(NC_UNSPECIFIED, MI_ORIGINAL_TYPE, [Unspecified NetCDF type])
# AC_DEFINE(NC_LONG, NC_INT, [Alias for NC_INT])
AC_CONFIG_FILES([Makefile])
AC_OUTPUT
