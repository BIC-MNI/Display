dnl Process this file with autoconf to produce a configure script.
AC_INIT
AC_CONFIG_SRCDIR([markers/markers.c])
AC_PREREQ(2.57)
AM_INIT_AUTOMAKE(Display,1.5.0)
AC_CONFIG_HEADERS([config.h])

dnl Optional workaround for buggy compiler.
AC_ARG_ENABLE([menu-fallback],
[  --disable-menu-fallback    Omit compiled-in fallback for menu.],
[if test "$enableval" = no; then
    AC_DEFINE(DISPLAY_DISABLE_MENU_FALLBACK,1,
              [define to disable compiled-in fallback menu])
 fi
])

dnl Optional support for SEAL.
AC_ARG_WITH([seal],
[  --with-seal              Enable support for sulcal labelling using SEAL.])
AM_CONDITIONAL(USE_SEAL, test x$with_seal = xyes)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_LN_S
AC_PROG_MAKE_SET

dnl Use libtool for linking
AM_PROG_LIBTOOL

dnl Find all the X stuff
AC_PATH_XTRA

dnl Add extra include or link directories
smr_WITH_BUILD_PATH

dnl Checks for header files.

dnl Checks for libraries.

smr_REQUIRED_LIB(m,sqrt,math.h)
mni_REQUIRE_BICPL
MNI_LIBS="$LIBS"
AC_SUBST(MNI_LIBS)

if test x$with_seal = xyes ; then
    mni_REQUIRE_LIB(bicseal,
                    [#include <bicseal/SULGRAPHDATA.h>
                     #include <bicseal/prototypes.h>],
                    [match_indice_with_enum(0,0)])
    AC_DEFINE(DISPLAY_USE_SEAL,1,
              [define to enable sulcal labelling using SEAL])
fi

dnl get OpenGL stuff
AX_CHECK_GL
AX_CHECK_GLU
AX_CHECK_GLUT

dnl check if we found them
if test "$no_gl" = yes -o "$no_glu" = yes -o "$no_glut" = yes ;
then
   AC_MSG_ERROR([
================================================================================
WARNING: GL/GLU/GLUT not found.

The GL, GLU, and GLUT libraries are required. (also check libXi and libXmu)
================================================================================
   ])
fi


AC_SUBST(GL_LIBS)
AC_SUBST(GL_CFLAGS)
AC_SUBST(GLU_LIBS)
AC_SUBST(GLU_CFLAGS)
AC_SUBST(GLUT_LIBS)
AC_SUBST(GLUT_CFLAGS)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

dnl Checks for library functions.
smr_OPTIONAL_LIB(malloc,mallinfo,malloc.h)
if test $HAVE_libmalloc = yes; then
    AC_DEFINE(HAVE_MALLINFO,1,[define if function mallinfo() is available])
    malloc_LIBS=-lmalloc
    AC_SUBST(malloc_LIBS)
fi


AC_CONFIG_FILES([Makefile
Documentation/Makefile
Graphics/Makefile
Graphics/GLUT_windows/Makefile
Graphics/G_graphics/Makefile
Graphics/OpenGL_graphics/Makefile
atlas/Makefile
callbacks/Makefile
current_obj/Makefile
cursor/Makefile
cursor_contours/Makefile
edit_surface/Makefile
events/Makefile
images/Makefile
immediate_mode/Makefile
input_files/Makefile
intersect/Makefile
main/Makefile
markers/Makefile
menu/Makefile
segmenting/Makefile
slice_window/Makefile
structures/Makefile
surface_curves/Makefile
surface_extraction/Makefile
tubes/Makefile
voxel_scan/Makefile
epm-header
])
AC_OUTPUT
